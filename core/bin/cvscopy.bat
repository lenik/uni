@echo off
rem $Id: cvscopy.bat,v 1.8 2005-07-14 02:27:30 dansei Exp $


    set _copy_for_retry=%0 %*
    set _cvsroot=%CVSROOT%
    set _cvsopts=
    set _module=CVSROOT
    set _base=

    if "%1"=="" goto help

:args

    if "%1"=="" goto args_x

    if /i "%1" equ "-d" (
        set _cvsroot=%2
        shift
        shift
        goto args
    )

    if /i "%1" equ "-m" (
        set _module=%2
        shift
        shift
        goto args
    )

    if /i "%1" equ "-n" (
        set _base=%2
        shift
        shift
        goto args
    )

    if /i "%1" equ "-o" (
        set _cvsopts=%2
        shift
        shift
        goto args
    )

    echo invalid argument "%1"
    goto end

:args_x

    if "%_cvsroot"=="" (
        echo CVSROOT is not specified.
        goto end
    )

    if "%_module"=="" (
        echo CVS Module is not specified.
        goto end
    )


rem 1, cvs-export

    set _tempdir=cvscopy.%random%
    mkdir %_tempdir%
    cd %_tempdir%

    rem ./_tempdir/ *module/base*

    echo cvs %_cvsopts% -d %_cvsroot% export -r HEAD %_module%
    cvs %_cvsopts% -d %_cvsroot% export -r HEAD %_module%

    if not exist "%_module%\*" (
        echo CVS export failed.
        goto end
    )


rem 2, "chroot" - move */module/base  to */base

    rem get the basename '%_module%'

    if "%_base%"=="" (
        for /d %%i in ("%_module%") do set _base=%%~nxi
    )

    if "%_base%"=="" (
        echo can't get basename of %_module%
    )

    rem only copy newer files, and overwrite Readonly files

    rem copy ./*module/base*/ to ./../*base-only*/
    xcopy /y /d /e /r "%_module%" ..\%_base%\

    cd..

    rd /s /q %_tempdir%

    rem make readonly, because it's a mirror-copy, should not be changed.

    attrib /s +r %_base%\*


rem 3, update .sync-cvscopy-%mod%.bat file
    rem if not exist .VEX\sync\* md .VEX\sync
    echo @echo off                >.sync-cvscopy-%CC_NAME%.bat
    echo rem CVSCOPY-Rev: $Revision: 1.8 $    >>.sync-cvscopy-%CC_NAME%.bat
    echo rem Thie file is auto generated by CVSCOPY Program, and it will >>.sync-cvscopy-%CC_NAME%.bat
    echo rem be automaticly updated. Please don't edit this file. >>.sync-cvscopy-%CC_NAME%.bat
    echo %_copy_for_retry%            >>.sync-cvscopy-%CC_NAME%.bat

    goto end


:help
    echo [CVSCOPY] CVS Local Copy and Synchronize
    echo Written by Snima Denik, Update $Date: 2005-07-14 02:27:30 $
    echo Syntax
    echo     cvscopy
    echo       -d (cvsroot)
    echo       -o (cvs options)
    echo       -m (module) [ /(path) ]
    echo       -n (basename-rename)
    echo Example
    echo    cvscopy -d :pserver:dansei@q:/crit/active/usnap -o -z3
    echo            -m bodzjava/bj-commons -n BJCommons
    goto end

:end
    set _copy_for_retry=
    set _cvsroot=
    set _cvsopts=
    set _module=
    set _base=
    set _tempdir=
