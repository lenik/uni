@rem = '
    @echo off

        if not "%OS%"=="Windows_NT" goto err_os

        call perl %~dpnx0 %*
        goto end

    :err_os
	echo You must run this program under Windows NT/2000 or above.
	goto end
    ';

#!/usr/bin/perl

use strict;
use vars qw/@ISA @EXPORT/;
use cmt::path;
use cmt::perlsys;
use cmt::util;
use cmt::vcs;
use DirHandle;
use Exporter;
use Getopt::Long;

sub _boot;
sub _main;
sub _use;
sub _require;
sub _info;
sub _info2;
sub _version;
sub _help;

@ISA = qw(Exporter);
@EXPORT = qw(
	_info
	_info2
	);

our $opt_verbtitle      = 'fun';
our $opt_verbtime       = 0;
our $opt_verbose        = 1;
our $opt_funname;

sub _boot {
    my @FARGV;
    for (my $i = 0; $i < scalar(@ARGV); $i++) {
        my $arg = $ARGV[$i];
        if ($arg =~ m/^[~\/]/) {
            @FARGV = splice(@ARGV, $i + 1);
            $opt_funname = substr(pop @ARGV, 1);
            last;
        }
    }
    GetOptions('quiet|q'    => sub { $opt_verbose-- },
               'verbose|v'  => sub { $opt_verbose++ },
               'version'    => sub { _version; exit },
               'help|h'     => sub { _help; exit },
               'use|u=s'    => sub { shift; _use shift },
               'require|r=s'=> sub { shift; _require shift },
               );

    die("fun name isn't specified") if $opt_funname eq '';

    _use $_ for @ARGV;

    @ARGV = @FARGV;
    $0 = $opt_funname;
    _info2 "fun name: $0";
    _info2 "fun arg: $_" for @ARGV;

    my $ret = _main;

    _info2 "fun $0 returns: $ret";
    return $ret;
}

sub _use {
    my $pkg = shift;
    $pkg = "fun:$pkg" if $pkg =~ m/^:/;
    _info2 "use $pkg; ";
    my $ret = eval("use $pkg; ");
    _info2 "use failed: $@" if $@ and not defined $ret;
}

sub _require {
    my $pkg = shift;
    _info "require $pkg; ";
    my $ret = eval("require $pkg; ");
    _info "use failed: $@" if $@ and not defined $ret;
}

sub _info {
    return if $opt_verbose < 1;
    my $text = shift;
    print datetime.' ' if $opt_verbtime;
    print "[$opt_verbtitle] $text\n";
}

sub _info2 {
    return if $opt_verbose < 2;
    my $text = shift;
    print datetime.' ' if $opt_verbtime;
    print "[$opt_verbtitle] $text\n";
}

sub _version {
    my %id = parse_id('$Id: fun,v 1.7 2007-07-10 15:43:00 lenik Exp $');
    print "[$opt_verbtitle] Perl simple function caller\n";
    print "Written by Lenik,  Version $id{rev},  Last updated at $id{date}\n";
}

sub _help {
    _version;
    print <<"EOM";

Syntax:
        $0 [<options>] [<use-package>...]
            ~<fun-name> [<fun-options>...]

Option:
        --use=<package> (u)
        --require=<package> (r)
        --quiet (q)
        --verbose (v, repeat twice give you more verbose info)
        --version
        --help (h)

Examples:
        fun ~hello world
EOM
}

exit _boot;

sub _main {
    my $sub = $0;
    # unless necessary libraries are used.
    unless (main->can($sub)) {
        for my $d (which_package('fun::')) {
            _info2 "scan for autoload: $d/";
            for ((new DirHandle($d))->read) {
                next unless s/\.pm$//;
                next unless $_ =~ /^\w+$/;
                _info2 "autoload fun::$_";
                eval("use fun::$_; 1")
                    or die("Can't load fun::$_: $!");
            }
        }
        die "Unknown fun $sub" unless main->can($sub);
    }

    $sub = eval('\&'.$sub);
    my $pkg = which_sub($sub);
    if ($pkg->can('boot')) {
        _info2 "boot $pkg";
        $pkg->boot;
    }
    $sub->(@ARGV);
}

__END__
:end
